#INCLUDE "TOTVS.CH"

#DEFINE NIVEL 1
#DEFINE SUBNIVEL 2

#DEFINE SINGLE "01"
#DEFINE ALL    "02"
#DEFINE INSERT "03"
#DEFINE DELETE "04"
#DEFINE UPDATE "05"
#DEFINE BUSCA  "07"

/*/{Protheus.doc} ${prefix}Request
    Classe abstrata que controla o corpo dos endpoints dos servicos rest do autorizador
    @type  Class
    @author victor.silva
    @since 20180427
/*/
Class ${prefix}Request
	
    Data oRest
    Data cSvcName
    Data cResponse
    Data cPropLote
    Data cFaultDesc
    Data cFaultDetail
    Data aExpand
    Data lSuccess
    Data nStatus
    Data nFault
    Data oReqBody
    Data oRespBody
    Data oRespControl
    Data oCollection
    Data jRequest
    Data oValidador

    Method New(oRest, cSvcName) Constructor

    Method destroy()
    Method checkAuth()
    Method applyFields()
    Method applyPageSize()
    Method checkBody()
    Method getSuccess()
    Method initRequest()
    Method endRequest()
    Method serializeFault()
    Method checkAgreement()
    Method procGet(nType)
    Method procDelete()
    Method procPost(cJson,oCmd)
    Method procLotePost(oCmd)
    Method procPut(oCmd)
    Method buildBody(oEntity)
    Method applyExpand()
    Method expand(oJson)    
    
EndClass

Method New(oRest, cSvcName) Class ${prefix}Request
    self:oRest        := oRest
    self:cSvcName     := cSvcName
    self:lSuccess     := .T.
    self:nStatus      := 200
    self:oReqBody     := JsonObject():New()
    self:oRespBody    := JsonObject():New()
    self:jRequest     := JsonObject():New()
    self:oRespControl := ${prefix}JsonControl():New()
    self:nFault       := 0
    self:cFaultDesc   := ''
    self:cResponse    := ''
    self:cFaultDetail := ''
    self:aExpand      := {}    
Return self

Method destroy() Class ${prefix}Request
    
    FreeObj(self:oReqBody)
    self:oReqBody := nil
    
    FreeObj(self:oRespBody)
    self:oRespBody := nil
    
    FreeObj(self:oRespControl)
    self:oRespControl := nil
    
    FreeObj(self:oCollection)
    self:oCollection := nil

    FreeObj(self:jRequest)
    self:jRequest := nil
    
    FreeObj(self:oValidador)
    self:oValidador := nil
    
Return

/*/{Protheus.doc} checkAuth
Valida as informacoes de autenticacao do usuario com as informacoes do header da requisicao
@author  victor.silva
@since   20180518
/*/
Method checkAuth() Class ${prefix}Request
    // TODO - IMPLEMENTAR LEITURA DO HEADER COM A AUTENTICACAO DO USUARIO
    // self:oRest:getHeader()
    // VarInfo("REST",ClassMethArr(self:oRest,.T.))
Return self:lSuccess

Method applyFields() Class ${prefix}Request
    self:oRespControl:prepFields(self:oRest:fields)
Return

/*/{Protheus.doc} checkAuth
Seta os os expandables solicitados na URL
@author  lima.everton
@since   20191025
/*/
Method applyExpand() Class ${prefix}Request
    If self:lSuccess
        if !empty(self:oRest:expand)
            self:aExpand := StrTokArr2(self:oRest:expand, ",")
            if self:oCollection:applyExpand(self:aExpand)
                self:lSuccess := .T.
            Else
                self:nFault := 400
                self:cFaultDesc := "Não foi possivel aplicar o(s) expandable(s)."
                self:cFaultDetail := "expand="+self:oRest:expand
                self:lSuccess := .F.
            Endif
        endif
    EndIf
Return self:lSuccess

/*/{Protheus.doc} applyPageSize
Aplica o tamanho da pagina para paginas do tipo colecao
@author  victor.silva
@since   20180523
/*/
Method applyPageSize() Class ${prefix}Request

    Default self:oRest:page := "1"
    Default self:oRest:pageSize := "20"
    
    self:oCollection:applyPageSize(self:oRest:page,self:oRest:pageSize)
    self:oRespBody["hasNext"] := .F.
    self:oRespBody["items"] := {}

Return self:lSuccess

/*/{Protheus.doc} checkBody
Realiza o parser do JSon enviado pelo client
@author  victor.silva
@since   20180518
/*/
Method checkBody() Class ${prefix}Request
    Local cParseError := ""

    // TODO - SYSLOG RFC-5424
    cParseError := self:jRequest:fromJson(DecodeUTF8(self:oRest:GetContent(), "cp1252"))

    if empty(cParseError)
         self:lSuccess  := .T.
    else
        self:nFault     := 400
        self:cFaultDesc   := "Não foi possível fazer o parse da mensagem."
        self:cFaultDetail := cParseError
        self:lSuccess   := .F.
    endif

Return self:lSuccess

Method getSuccess() Class ${prefix}Request
Return .T.

/*/{Protheus.doc} endRequest
Inicializa a solicitacao
@author  victor.silva
@since   20180518
/*/
Method initRequest() Class ${prefix}Request
    // TODO - SYSLOG RFC-5424
Return

/*/{Protheus.doc} endRequest
Finaliza a solicitacao e coloca a resposta e status na requisicao
@author  victor.silva
@since   20180518
/*/
Method endRequest() Class ${prefix}Request
    Local cResponse := ""
    if self:lSuccess
        self:oRest:setStatus(self:nStatus)
        cResponse := EncodeUTF8(self:cResponse)
        self:oRest:setResponse(cResponse)
    else
        self:oRest:setStatus(self:nStatus)
        cResponse := EncodeUTF8(self:serializeFault())
        self:oRest:setResponse(cResponse)
        // TODO - AutSysLog: dinamizar o tenantId quando prepararmos a aplicação para trabalhar com tenant
        //AutSysLog(ProcName(), RESTAPI, INFORMATIONAL, 1, self:cMsgId, "[tenantId=1]", {"Finalizando requisição com erro: " + cResponse })
    endif
Return

/*/{Protheus.doc} serializeFault
Serializa retorno de falha do WSREST
@author  victor.silva
@since   20180704
/*/
Method serializeFault() Class ${prefix}Request

    Local oJson := JsonObject():New()

    oJson["code"] := self:nFault
    oJson["message"] := self:cFaultDesc
    oJson["detailedMessage"] := self:cFaultDetail
    oJson["helpUrl"] := ""
    oJson["details"] := {}
    aAdd(oJson["details"], JsonObject():New())
    oJson["details"][1]["code"] := ""
    oJson["details"][1]["message"] := ""
    oJson["details"][1]["detailedMessage"] := ""
    oJson["details"][1]["helpUrl"] := ""

Return oJson:toJson()

/*/{Protheus.doc} serializeFault
Percorre a lista de filhos
@author  lima.everton
@since   2019102019
/*/
Method expand(oJson) Class CenRequest
    
    Local nExp := 1
    Local aExpand := {}
    Local oCltSub := Nil
    Default oJson := JsonObject():New()

    For nExp:= 1 to Len(self:aExpand)

        aExpand := StrTokArr2(self:aExpand[nExp],".")
        oJson[aExpand[NIVEL]] := {}
        
        self:oCollection:getRelation(aExpand[NIVEL]):buscar()
        if self:oCollection:getRelation(aExpand[NIVEL]):found()
            while self:oCollection:getRelation(aExpand[NIVEL]):hasNext()
                
                oEntity := self:oCollection:getRelation(aExpand[NIVEL]):getNext()
                aAdd(oJson[aExpand[NIVEL]], self:buildBody(oEntity))
                
                If Len(aExpand) > 1 //Expande o subnivel
                    oJson[aExpand[NIVEL]][NIVEL][aExpand[SUBNIVEL]] := {}
                    oCltSub := self:oCollection:getRelation(aExpand[NIVEL]):getRelation(aExpand[SUBNIVEL])
                    oCltSub:buscar()
                    if oCltSub:found()
                        while oCltSub:hasNext()
                            oEntity := oCltSub:getNext()
                            aAdd(oJson[aExpand[NIVEL]][NIVEL][aExpand[SUBNIVEL]], self:buildBody(oEntity))
                        enddo
                    EndIf
                EndIf
                
            enddo
        EndIf

    Next

    oEntity:destroy()

Return oJson

Method checkAgreement() class ${prefix}Request
Return .T.

Method procGet(nType) Class ${prefix}Request

    Local nRegistro := 1
    Local oEntity := nil
    Local oJson := nil

    If self:lSuccess
        if self:oCollection:found()
            If nType == ALL
                while self:oCollection:hasNext() .And. nRegistro <= Val(self:oCollection:getPageSize())
                    oEntity := self:oCollection:getNext()
                    oEntity:setHashFields(self:oRespControl:hmFields)
                    oJson := self:expand(self:buildBody(oEntity))
                    aAdd(self:oRespBody['items'], oJson)
                    nRegistro++
                enddo
                self:oRespBody["hasNext"] := self:oCollection:hasNext()
                self:cResponse := self:oRespBody:toJson()
            Else
                oEntity := self:oCollection:getNext(oEntity)
                oEntity:setHashFields(self:oRespControl:hmFields)
                oJson := self:expand(self:buildBody(oEntity))
                self:jRequest := oJson
                self:cResponse := self:jRequest:toJson()
            Endif
            oEntity:destroy()
        Else
            If nType == ALL
                self:oRespBody["hasNext"] := self:oCollection:hasNext()    
            Else
                self:oRespBody := JsonObject():New()
            EndIf
            self:cResponse := self:oRespBody:toJson()
        Endif
    
    Endif

    FreeObj(oEntity)
    oEntity := Nil

Return self:lSuccess

Method procDelete() Class ${prefix}Request

    Local nI := 0
    Local aRelList := {}

    aRelList := self:oCollection:initRelation()
    
    If self:lSuccess
        self:oCollection:setKeyRelation()
        For nI := 1 to Len(aRelList)
            if aRelList[nI][COLLECTION]:getBehavior() == CASCADE
                self:oCollection:getRelation(aRelList[nI][RELNAME]):delRelation()
            EndIf
        Next
        If (self:oCollection:delete())
            self:nStatus := 204
            self:cResponse := ""
        EndIf
    Else
        self:nFault       := 400
        self:nStatus      := 400
        self:cFaultDesc   := "Operação não pode ser realizada."
        self:cFaultDetail := "Registro não existe no banco."        
    EndIf

Return self:lSuccess

Method procPost() Class ${prefix}Request
    
    Local nI := 1
    Local nJ := 1
    Local nQtdExpand := 0
    Local oEntity := nil
    Local oCltRel := nil
    Local cJsonResp := ""
    Local aRelList := {}
    Local oCmd := CenCmdPostClt():New(self:oCollection)

    aRelList := self:oCollection:initRelation()
   
    if self:lSuccess
        self:prepFilter(self:jRequest)
        self:buscar(INSERT)
        If self:lSuccess
            If self:oValidador:validate(self:oCollection) //Validate
                If oCmd:execute()
                    For nI:= 1 to Len(aRelList)
                        oCltRel := self:oCollection:getRelation(aRelList[nI][RELNAME])
                        For nJ:= 1 to Len(self:jRequest[aRelList[nI][RELNAME]])
                            oCltRel:mapFromJson(self:jRequest[aRelList[nI][RELNAME]][nJ])
                            oCmd := CenCmdPostClt():New(oCltRel)
                            oCmd:execute()
                        Next
                    Next
                    self:nStatus := 201
                    If self:buscar(SINGLE)
                        nJ := 1
                        nQtdExpand := len(self:oCollection:oMapper:aExpand)
                        self:aExpand := Array(1,nQtdExpand)
                        For nJ := 1 to nQtdExpand
                            self:aExpand[nJ] := self:oCollection:oMapper:aExpand[nJ][RELNAME]
                        Next
                        oEntity := self:oCollection:getNext()
                        self:oRespBody := oEntity:serialize(self:oRespControl)
                        self:oRespBody := self:expand(self:oRespBody)
                        cJsonResp := self:oRespBody:toJson()
                    EndIf
                    self:cResponse := cJsonResp
                Else
                    self:lSuccess     := .F. 
                    self:nFault       := 400
                    self:nStatus      := 400
                    self:cFaultDesc   := "Operação não pode ser realizada."
                    self:cFaultDetail := "Erro ao realizar insert."
                Endif
            Else
                self:lSuccess     := .F. 
                self:nFault       := 400
                self:nStatus      := 400
                self:cFaultDesc   := "Operação não pode ser realizada."
                self:cFaultDetail := self:oValidador:getErrMsg()
            EndIf //Fim Validate
        Else
            self:nFault       := 400
            self:nStatus      := 400
            self:cFaultDesc   := "Operação não pode ser realizada."
            self:cFaultDetail := "Registro já existe no banco."
        EndIf
    Endif

    FreeObj(oEntity)
    oEntity := Nil

Return self:lSuccess

Method procLotePost(oCmd) Class ${prefix}Request

    Local nMinLimit     := 1
    Local nMaxLimit     := 100
    Local nRegistro     := 1
    Local nCount        := 0
    Local oEntity       := 0
    Local jAlreadyExists:= JsonObject():New()        
    Local jErrors       := JsonObject():New()
    Local jSerialize    := AutJsonControl():New()
    Local jLoteResponse := JsonObject():New()
    Default oCmd := CenCmdPostClt():New(self:oCollection)

    self:oRespBody      := nil
    
    jSerialize:newArray(jLoteResponse, 'included')
    jSerialize:newArray(jLoteResponse, 'notIncluded')

    jSerialize:newArray(jAlreadyExists, self:cPropLote)
    jSerialize:setProp(jAlreadyExists, 'codeError', 400)
    jSerialize:setProp(jAlreadyExists, 'errorMessage', 'Beneficiário(s) Já existe(m).')

    jSerialize:newArray(jErrors, self:cPropLote)
    jSerialize:setProp(jErrors, 'codeError', 400)
    jSerialize:setProp(jErrors, 'errorMessage', 'Erro ao tentar inserir Beneficiário(s).')
           
    If self:lSuccess
        If ValType(self:jRequest) == "A"
            nCount := Len(self:jRequest)
            If nCount >= nMinLimit .AND. nCount <= nMaxLimit
                For nRegistro := 1 to nCount
                    self:prepFilter(self:jRequest[nRegistro])
                    self:applyFilter(SINGLE)
                    self:buscar(INSERT)
                    If self:lSuccess
                        If oCmd:execute()
                            If self:buscar(BUSCA)
                                oEntity := self:oCollection:getNext()
                                jSerialize:addObjtoProp(jLoteResponse, 'included', oEntity:serialize(self:oRespControl))
                                oEntity:destroy()
                            EndIf
                        Else
                            jSerialize:addObjtoProp(jErrors, self:cPropLote, self:jRequest[nRegistro])
                        EndIf
                    Else
                        jSerialize:addObjtoProp(jAlreadyExists, self:cPropLote, self:jRequest[nRegistro])
                    EndIf
                    self:oRespControl := Nil
                Next

                If len(jAlreadyExists["beneficiaries"]) >= 1
                    aAdd(jLoteResponse["notIncluded"], jAlreadyExists)    
                EndIf
                If len(jErrors["beneficiaries"]) >= 1
                    aAdd(jLoteResponse["notIncluded"], jErrors)
                EndIf
                
                self:lSuccess  := .T.
                self:nStatus   := 200
                self:cResponse := jLoteResponse:toJson()

            Else 
                self:lSuccess     := .F.
                self:nStatus      := 400
                self:nFault       := 400
                self:cFaultDesc   := "Operação não pode ser realizada."
                self:cFaultDetail := "Operação em lote só é permitida de " + cValToChar(nMinLimit) + " a " + cValToChar(nMaxLimit) + " registros."
            EndIf
        Else
            self:lSuccess     := .F.
            self:nStatus      := 400
            self:nFault       := 400
            self:cFaultDesc   := "Operação não pode ser realizada."
            self:cFaultDetail := "Operação só pode ser realizada em lote, informe um array de objetos válidos."
        EndIf
    EndIf

    FreeObj(oEntity)
    FreeObj(jLoteResponse)
    FreeObj(jSerialize)
    FreeObj(jAlreadyExists)
    FreeObj(jErrors)
    oEntity := Nil
    jLoteResponse := Nil
    jSerialize := Nil
    jAlreadyExists := Nil
    jErrors := Nil

Return self:lSuccess

Method procPut(oCmd) Class ${prefix}Request

    Local nI := 1
    Local nJ := 1
    Local oEntity := nil
    Local aRelList := {}
    Default oCmd := CenCmdPutClt():New(self:oCollection)

    aRelList := self:oCollection:initRelation()
    
    If self:lSuccess
        self:prepFilter()
        self:applyFilter(SINGLE)
        self:buscar(SINGLE)
        If self:lSuccess
            If self:oValidador:validate(self:oCollection) //Validate
                self:oCollection:mapDaoJson(self:jRequest)
                If oCmd:execute()
                    For nI:= 1 to Len(aRelList)
                        oCltRel := self:oCollection:getRelation(aRelList[nI][RELNAME])
                        For nJ:= 1 to Len(self:jRequest[aRelList[nI][RELNAME]])
                            oCltRel:mapFromJson(self:jRequest[aRelList[nI][RELNAME]][nJ])
                            If oCltRel:bscChaPrim()
                                oCmd := CenCmdPutClt():New(oCltRel)
                                oCmd:execute()
                            EndIf
                        Next
                    Next
                    self:nStatus := 200
                    If self:buscar(SINGLE)
                        nJ := 1
                        nQtdExpand := len(self:oCollection:oMapper:aExpand)
                        self:aExpand := Array(1,nQtdExpand)
                        For nJ := 1 to nQtdExpand
                            self:aExpand[nJ] := self:oCollection:oMapper:aExpand[nJ][RELNAME]
                        Next
                        oEntity := self:oCollection:getNext()
                        self:oRespBody := oEntity:serialize(self:oRespControl)
                        self:oRespBody := self:expand(self:oRespBody)
                        cJsonResp := self:oRespBody:toJson()
                    EndIf
                    self:cResponse := cJsonResp
                Else
                    self:lSuccess     := .F. 
                    self:nFault       := 400
                    self:nStatus      := 400
                    self:cFaultDesc   := "Operação não pode ser realizada."
                    self:cFaultDetail := "Erro ao realizar update."
                Endif
            EndIf //Fim Validate
        Else
            self:nFault       := 404
            self:nStatus      := 404
            self:cFaultDesc   := "Operação não pode ser realizada."
            self:cFaultDetail := "Registro não encontrado."
        EndIf
    Endif

    FreeObj(oEntity)
    oEntity := Nil
 
Return self:lSuccess

Method buildBody(oEntity) Class ${prefix}Request
Return oEntity:serialize(self:oRespControl)